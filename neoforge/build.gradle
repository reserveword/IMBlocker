plugins {
    id 'eclipse'
    id 'idea'
    id 'net.neoforged.gradle.userdev' version '7.0.142'
}

// Mojang ships Java 21 to end users starting in 1.20.5, so mods should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

runs {
    client {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'
        systemProperty 'forge.enabledGameTestNamespaces', mod_id
        modSource sourceSets.main

        dependencies {
            runtime project(":common")
            runtime project(":forge")
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"
    
	compileOnly "maven.modrinth:sodium:mc1.21.4-0.6.13-neoforge"
    compileOnly "maven.modrinth:reeses-sodium-options:mc1.21.4-1.8.3+neoforge"

    implementation project(':common')
    implementation project(':forge')
}

project.jar {
    archiveFileName = "${rootProject.name}-${mod_version}-neoforge-1.20.6+.jar"
    from sourceSets.main.output
    from (project(":common").sourceSets.main.output) {
        exclude 'imblocker-old.png'
    }
    from (project(":forge").sourceSets.main.output) {
        include '**/ForgeCommon.class'
        include '**/mixin/*.class'
        exclude '**/mixin/AbstractScrollWidgetMixin.class'
        exclude '**/mixin/RSOAbstractFrameMixin.class'
    }
}